#!/usr/bin/perl
# 
#   File:		weather_cgi
#   Date:		03 Oct 2015 20:37:57
#   Author:		Alexander Zangerl <az@snafu.priv.at>
# 
#   Abstract:	cgi script for displaying the current weather and pre-made graphs
#
use strict;
use CGI;
use SQL::Abstract;
use DateTime;
use DBI;
use Template;

# configurables: none but the config file location
my $conffile = "/etc/weather.conf";
my %conf = eval { do "$conffile"; };
die "cannot read or parse $conffile: $@ $!\n" if ($@ or !keys %conf);

my $q = CGI->new;
my $sitekey = $q->path_info;
$sitekey =~ s!^/([^/]+).*$!$1!;

if (!$conf{sites}->{$sitekey})
{
    print $q->header(-status => 400), $q->start_html("No such site"),
    "You are trying to access a non-existent site.", $q->end_html;
    exit 0;
}

my $db=DBI->connect("dbi:SQLite:dbname=".$conf{dbfile});
die "can't connect to db: $!\n" if (!$db);
my $sql=SQL::Abstract->new;

my $template = join("", <DATA>);
my $t = Template->new({INTERPOLATE => 1, EVAL_PERL => 1});

# pull in the newest reading
my $newest = get_weather_at($sitekey, undef);
my $dtn = DateTime->from_epoch(epoch => $newest->{time},
															 time_zone => $conf{displaytz});

my %tvars = ( newest => $dtn->strftime($conf{datefmt}),
							sitename => $conf{sites}->{$sitekey}->{name},
							sitekey => $sitekey,

							wind_dir_avg1_name => deg2name($newest->{wind_dir_avg1}),
							wind_dir_avg2_name => deg2name($newest->{wind_dir_avg2}),
							wind_dir_avg10_name => deg2name($newest->{wind_dir_avg10}),
							wind_dir_gust10_name => deg2name($newest->{wind_dir_gust10}),
							
							ploturlbase => $conf{ploturlbase} );

# limit the speed floats to one digit
# and replace any undefs with N/A
for my $k (keys %$newest)
{
	if (!defined $newest->{$k})
	{
		$tvars{$k} = "N/A";
	}
	else
	{
		$tvars{$k} = ($k =~ /^wind_speed/)? sprintf("%.1f", $newest->{$k}) : $newest->{$k};
	}
}

# get the rain data for the last 14 days
for my $daysago (1..14)
{
	my $dayend = $dtn->clone->truncate(to => "day")->subtract(days => $daysago-1, nanoseconds => 1); # 23:59:59.99... of the day before
	$tvars{"day$daysago"}	= $dayend->strftime("%a");
	$tvars{"day${daysago}_ago"} = ($daysago == 1)?"yesterday": "$daysago days ago";

	# that gets the nearest reading
	my $weatherthen = get_weather_at($sitekey, $dayend->epoch);
	$tvars{"day${daysago}_rain"} = ($weatherthen)? sprintf("%.1f mm",$weatherthen->{rain_24h}): "N/A";
}

print $q->header(-cache_control => "public, max-age=900");
$t->process(\$template, \%tvars) or die $t->error;
exit 0;

# note: this is 100% pulled from weatherreceiver
# returns the weather hash near given time X or newest
# args: site, time (or undef for newest)
# returns weather hash ref or undef
sub get_weather_at
{
    my ($site, $when) = @_;

    # specific time
    if ($when)
    {
	my $precise = $db->selectrow_hashref("select time from $site where time = $when",
					     {MaxRows=>1});
	if ($precise && $precise->{time})
	{
	    $when = $precise->{time};
	}
	else
	{
	    # this may pick any reading within +/- 30s.
	    my $nearest = $db->selectrow_hashref("select time from $site where abs(time-$when) < 30",
						 {MaxRows=>1});
	    return undef if (!$nearest or !$nearest->{time});
	    $when = $nearest->{time};
	}
    }
    # get latest observation
    else
    {
			my $latest=$db->selectrow_hashref("select MAX(time) AS time from $site",
																				{MaxRows=>1});
			return undef if (!$latest or !$latest->{time});
			$when=$latest->{time};
    }

    # get all the data at given time, as-is, no averaging
    my ($stmt,@bind)=$sql->select($site,'*',
				  {"time"=>$when});
    my $res=$db->selectrow_hashref($stmt,{MaxRows=>1},@bind);

    return $res;
}

sub deg2name
{
    my ($deg)=@_;

    my @names=(qw(N NNE NE ENE E ESE SE SSE S SSW SW WSW W WNW NW NNW));
    my $i=int(($deg*16/360)+.5)%16;
    return $names[$i];
}

# html template goes here, read from the DATA filehandle
__DATA__
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<head>
  <head>
    <title>Weather Observations at ${sitename}</title>
  </head>
  
  <body>
    <h1>${sitename}</h1>
    <h2>Overview at ${newest}</h2>

    <table border="1" style="float: left; margin-right: 10px;">
      <tr><td>Barometric Pressure</td><td>${barometer} hPa</td></tr>
      <tr><td>Temperature</td><td>${outside_temp}° C</td></tr>
      <tr><td>Relative Humidity</td><td>${outside_hum} %</td></tr>
    </table>

		
    <table border="1" style="float: left; margin-right: 10px;">
			<tr><th colspan="4">Wind</th></tr>
      <tr><th>When</th><th colspan="2">Direction</th><th>Speed</th></tr>
      <tr><td>1 min avg.</td><td>${wind_dir_avg1_name}</td><td>${wind_dir_avg1}°</td><td>${wind_speed_avg1} km/h</td></tr>
      <tr><td>2 min avg.</td><td>${wind_dir_avg2_name}</td><td>${wind_dir_avg2}°</td><td>${wind_speed_avg2} km/h</td></tr>
      <tr><td>10 min avg.</td><td>${wind_dir_avg10_name}</td><td>${wind_dir_avg10}°</td><td>${wind_speed_avg10} km/h</td></tr>
      <tr><td>10 min gust</td><td>${wind_dir_gust10_name}</td><td>${wind_dir_gust10}°</td><td>${wind_speed_gust10} km/h</td></tr>
    </table>

    <table border="1" style="float: left; margin-right: 10px;">
			<tr><th colspan="2" style="text-align: left">Rain Today</th></tr>
      <tr title="This row indicates the severity of the current rain fall. The rain rate it is most recent rain measurement extrapolated to a full hour."><td>Current Rate</td><td>${rain_rate} mm/hr</td></tr>
      <tr title="This is the total rainfall (in mm per m^2) during the last quarter hour."><td>Last 15 min</td><td>${rain_15m} mm</td></tr>
      <tr title="This is the total rainfall (in mm per m^2) during the last full hour."><td>Last 60 min</td><td>${rain_60m} mm</td></tr>
      <tr title="This row shows the total rain fallen in the last 24 hours (*not* since midnight)."><td>Last 24 hours</td><td>${rain_24h} mm</td></tr>
    </table>

		<table border="1" style="float: left; margin-right: 10px;">
			<tr><th colspan="4" style="text-align: left">Recent Rain</th></tr>
			<tr><th>Last 7 Days</th><th>Daily Rain</th><th>Week Before</th><th>Daily Rain</th></tr>
			<tr><td>${day1} (${day1_ago})</td><td>${day1_rain}</td><td>${day8} (${day8_ago})</td><td>${day8_rain}</td></tr>
			<tr><td>${day2} (${day2_ago})</td><td>${day2_rain}</td><td>${day9} (${day9_ago})</td><td>${day9_rain}</td></tr>
			<tr><td>${day3} (${day3_ago})</td><td>${day3_rain}</td><td>${day10} (${day10_ago})</td><td>${day10_rain}</td></tr>
			<tr><td>${day4} (${day4_ago})</td><td>${day4_rain}</td><td>${day11} (${day11_ago})</td><td>${day11_rain}</td></tr>
			<tr><td>${day5} (${day5_ago})</td><td>${day5_rain}</td><td>${day12} (${day12_ago})</td><td>${day12_rain}</td></tr>
			<tr><td>${day6} (${day6_ago})</td><td>${day6_rain}</td><td>${day13} (${day13_ago})</td><td>${day13_rain}</td></tr>
			<tr><td>${day7} (${day7_ago})</td><td>${day7_rain}</td><td>${day14} (${day14_ago})</td><td>${day14_rain}</td></tr>
		</table>
			
		<div style="clear: both">
			[% IF tx_batt_low == "TRUE" %]
			<p><strong>Attention: Transmitter battery is Low!</strong></p>
			[% END %]
			[% IF tx_down == "TRUE" %]
			<p><strong>Attention: Transmitter is Down!</strong></p>
			[% END %]
		</div>

    <h2>Graphs</h2>
    [% FOREACH graphtype IN [ "baro", "temp", "hum", "wind", "rain" ] %]
    <div>
      <img src="${ploturlbase}/${graphtype}_${sitekey}_2h.png">
      <img src="${ploturlbase}/${graphtype}_${sitekey}_24h.png">
    </div>
    [% END %]
    <div><img src="${ploturlbase}/rain_${sitekey}_336h.png"></div>
  </body>
</html>

